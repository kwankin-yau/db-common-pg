plugins {
    id 'java-library'
    id 'scala'
    id 'maven-publish'
    id 'signing'
}

group 'com.lucendar'
version '2.1.0-SNAPSHOT'

compileJava.options.encoding = 'UTF-8'
compileScala.options.encoding = 'UTF-8'

sourceCompatibility = '17'
targetCompatibility = '17'


repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

    implementation('org.scala-lang:scala-library:2.13.12')
    implementation('org.scala-lang:scala-reflect:2.13.12')

    implementation('com.typesafe.scala-logging:scala-logging_2.13:3.9.5')

    api 'com.lucendar:db-common:2.1.0-SNAPSHOT'

    api 'org.postgresql:postgresql:42.7.2'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = 'db-common-pg'
                packaging = 'jar'
                // optionally artifactId can be defined here
                description = 'Spring Jdbc utility library for PostgreSQL.'
                url = 'https://github.com/kwankin-yau/db-common-pg'

                scm {
                    connection = 'https://github.com/kwankin-yau/db-common-pg.git'
                    developerConnection = 'https://github.com/kwankin-yau/db-common-pg.git'
                    url = 'https://github.com/kwankin-yau/db-common-pg'
                }

                licenses {
                    license {
                        name = 'MIT'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'kky'
                        name = 'kwankin-yau'
                        email = 'alphax@vip.163.com'
                    }
                }
            }
        }
    }
}

javadoc {
    options.encoding = 'UTF-8'

    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

signing {
    sign publishing.publications.maven
}
